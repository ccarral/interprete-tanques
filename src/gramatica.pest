WHITESPACE = _{ " " | "\n" }

oper = _{ suma | resta | mult | div }
    suma = { "+" }
    resta = { "-" }
    mult = { "*" }
    div = { "/" }

expr = { term ~ (oper ~ term)* }

term = _{ int  | expr_par | nom_var }

int = @{ ("-")* ~ ASCII_DIGIT+ }

expr_par = {"(" ~ expr ~")"}

comp = _{ men_ig | men  | may_ig | may | ig | no_ig }
    men = @{ "<" }
    men_ig = @{ "<" ~ "=" }
    may = @{ ">" }
    may_ig = @{ ">" ~ "=" }
    ig = @{ "==" }
    no_ig = @{  "!=" }

oper_logic = _{ or | and }
    or = @{ "||" }
    and = @{ "&&" }

expr_par_logic = {"(" ~ expr_logic ~ ")"}

val = { int | expr | nom_var }

comp_logic = { (val ~ comp ~ val) }

term_logic = { comp_logic | expr_par_logic }

expr_logic = { term_logic ~ (oper_logic ~ term_logic)* }

nom_var = @{ ASCII_ALPHA_LOWER+ }

decl = { "var" ~ nom_var ~  "=" ~ expr ~ ";" }

asig = { nom_var ~ "=" ~ expr ~ ";" }

inst = _{ decl | asig | bloque_si | bloque_mientras }

bloque_si = { "si" ~ "(" ~ expr_logic ~ ")" ~ "{" ~ si_inner_bloque ~ "}"}

bloque_mientras= { "mientras" ~ "(" ~ expr_logic ~ ")" ~ "{" ~ si_inner_bloque ~ "}"}

bloque = _{ (inst)* ~ inst?}

si_inner_bloque = { bloque }

prog = _{
    SOI ~
    bloque ~
    EOI
}
